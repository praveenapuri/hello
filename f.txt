data "aws_instances" "windows" {
  filter {
    name = "platform"
    values = ["windows"]
  }
}

data "aws_instance" "instance_details" {
  for_each    = toset(data.aws_instances.all_instances.ids)
  instance_id = each.key
}
resource "aws_cloudwatch_metric_alarm" "linux_instance_disk_high" {
  # All non-Windows instances get an alarm
  for_each = {
    for id, inst in data.aws_instance.instance_details :
    id => inst
    if inst.platform != "windows"
  }

  alarm_name        = "linux_high_disk_usage_${each.key}"
  alarm_description = "CRITICAL | Epic Payer Platform | Linux Disk Usage High >= 80% on ${lookup(each.value.tags, "Name", each.key)}"
  alarm_actions     = [local.us_east_1_alarm_funnel]

  namespace           = "CWAgent"
  metric_name         = "disk_used_percent"
  statistic           = "Average"
  period              = 300
  evaluation_periods  = 1
  threshold           = 80
  comparison_operator = "GreaterThanOrEqualToThreshold"
  treat_missing_data  = "notBreaching"
  actions_enabled     = true

  # Root filesystem (change to /epic, etc., if you prefer)
  dimensions = {
    InstanceId = each.key
    path       = "/"
  }

  tags = merge(local.tags, local.cigna_tags)
}

resource "aws_cloudwatch_metric_alarm" "linux_instance_memory_high" {
  for_each = {
    for id, inst in data.aws_instance.instance_details :
    id => inst
    if inst.platform != "windows"
  }

  alarm_name        = "linux_high_memory_usage_${each.key}"
  alarm_description = "CRITICAL | Epic Payer Platform | Linux Memory Usage High >= 80% on ${lookup(each.value.tags, "Name", each.key)}"
  alarm_actions     = [local.us_east_1_alarm_funnel]

  namespace           = "CWAgent"
  metric_name         = "mem_used_percent"
  statistic           = "Average"
  period              = 300
  evaluation_periods  = 1
  threshold           = 80
  comparison_operator = "GreaterThanOrEqualToThreshold"
  treat_missing_data  = "notBreaching"
  actions_enabled     = true

  dimensions = {
    InstanceId = each.key
  }

  tags = merge(local.tags, local.cigna_tags)
}
