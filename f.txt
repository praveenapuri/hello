# All running instances (both Windows + Linux)
data "aws_instances" "all_running" {
  filter {
    name   = "instance-state-name"
    values = ["running"]
  }
}

locals {
  # Linux = all_running - windows
  linux_instance_ids = setsubtract(
    toset(data.aws_instances.all_running.ids),
    toset(data.aws_instances.windows.ids),
  )
}

# Get full details for each Linux instance
data "aws_instance" "linux_details" {
  for_each    = local.linux_instance_ids
  instance_id = each.key
}
resource "aws_cloudwatch_metric_alarm" "linux_instance_disk_high" {
  for_each = data.aws_instance.linux_details

  alarm_name        = "linux_high_disk_usage_${each.key}"
  alarm_description = "CRITICAL | Epic Payer Platform | Linux Disk Usage High >= 80% on ${lookup(each.value.tags, "Name", each.key)}"
  alarm_actions     = [local.us_east_1_alarm_funnel]

  namespace           = "CWAgent"
  metric_name         = "disk_used_percent"
  statistic           = "Average"
  period              = 300
  evaluation_periods  = 1
  threshold           = 80
  comparison_operator = "GreaterThanOrEqualToThreshold"
  treat_missing_data  = "notBreaching"
  actions_enabled     = true

  # Watching root filesystem; change path if you want a different one
  dimensions = {
    InstanceId = each.key
    path       = "/"
  }

  tags = merge(local.tags, local.cigna_tags)
}
resource "aws_cloudwatch_metric_alarm" "linux_instance_memory_high" {
  for_each = data.aws_instance.linux_details

  alarm_name        = "linux_high_memory_usage_${each.key}"
  alarm_description = "CRITICAL | Epic Payer Platform | Linux Memory Usage High >= 80% on ${lookup(each.value.tags, "Name", each.key)}"
  alarm_actions     = [local.us_east_1_alarm_funnel]

  namespace           = "CWAgent"
  metric_name         = "mem_used_percent"
  statistic           = "Average"
  period              = 300
  evaluation_periods  = 1
  threshold           = 80
  comparison_operator = "GreaterThanOrEqualToThreshold"
  treat_missing_data  = "notBreaching"
  actions_enabled     = true

  dimensions = {
    InstanceId = each.key
  }

  tags = merge(local.tags, local.cigna_tags)
}
